import @objects::{ Object, Line, Circle, Rect, Material }
import @image::{ FImage, FColor }

import @ray::{ Ray, HitRec }
import @light::{ Light }

import std::vec::{ Vec2f }
import std::vector::{ Vector }

struct Scene {
    height: u32
    width: u32

    x_scale: f32
    y_scale: f32

    max_time: f32

    objects: &Vector<&Object>
    light: Light

    img: &FImage
}

enum DrawMode {
    Normal
    AntiAlias
}

def Scene::free(&this) {
    this.img.free()
    for obj : .objects.iter() {
        std::libc::free(obj.data)
        std::libc::free(obj)
    }
    .objects.free()
}

def Scene::draw_debug(&this) {
    for obj : .objects.iter() {
        .draw_debug_obj(obj)
    }
}

def Scene::clear_image(&this) {
    .img.clear()
}

def Scene::draw_debug_obj(&this, obj: &Object) {
    match obj.type {
        Line => {
            let ln = obj.data as &Line
            .draw_line(ln.start, ln.end, FColor(1.0, 1.0, 1.0), Normal, replace: true)
        }
        Circle => {
            let c = obj.data as &Circle
            .draw_circle(c.center, c.radius, FColor(1.0, 1.0, 1.0), Normal, replace: true)
        }
        Rect => {
            // Don't draw the background rect
            if obj == .objects.at(0) {
                return
            }

            let r = obj.data as &Rect

            let offset = 0.01

            let lines: [Line; 4]
            lines[0] = Line(r.min, Vec2f(r.max.x, r.min.y).subs(offset))
            lines[1] = Line(Vec2f(r.max.x, r.min.y), r.max.subs(offset))
            lines[2] = Line(r.max, Vec2f(r.min.x, r.max.y).subs(offset))
            lines[3] = Line(Vec2f(r.min.x, r.max.y), r.min.subs(offset))

            for let i = 0; i < 4; ++i {
                .draw_line(lines[i].start, lines[i].end, FColor(1.0, 1.0, 1.0), AntiAlias, replace: true)
            }
        }
    }
}

def Scene::new(width: u32, height: u32, max_time: f32 = -1.0): &Scene {
    let img = FImage::new(width, height)
    let objects = Vector<&Object>::new()

    let x_scale = 2.0
    let y_scale = 2.0

    if width > height {
        x_scale *= (width as f32) / (height as f32)
    } else {
        y_scale *= (height as f32) / (width as f32)
    }

    let scene = std::new<Scene>()
    scene.width = width
    scene.height = height
    scene.x_scale = x_scale
    scene.y_scale = y_scale
    scene.objects = objects
    scene.img = img
    scene.max_time = max_time

    scene.objects.push(Rect::new(Vec2f(-x_scale, -y_scale), Vec2f(x_scale, y_scale), Diffuse))
    return scene
}

def Scene::hit(&this, ray: &Ray, rec: &HitRec): bool {
    let hit = false
    for obj : .objects.iter() {
        if obj.hit(ray, rec) {
            hit = true
        }
    }
    return hit
}

def Scene::draw_circle(&this, p1: Vec2f, r: f32, color: FColor, mode: DrawMode, replace: bool = false, steps: u32 = 1000) {
    for let angle = 0; angle < steps; angle += 1 {
        let a = (angle as f32 / steps as f32) * 2.0 * 3.1415
        let x1 = p1.x + r * a.cos()
        let y1 = p1.y + r * a.sin()

        let x2 = (x1 + .x_scale) * (this.width as f32) / (2.0 * this.x_scale)
        let y2 = (y1 + .y_scale) * (this.height as f32) / (2.0 * this.y_scale)

        if mode == Normal {
            .draw_pixel(x2, y2, color, replace)
        } else {
            .draw_pixel_aa(x2, y2, color, replace)
        }
    }
}

def Scene::draw_line(&this, p1: Vec2f, p2: Vec2f, color: FColor, mode: DrawMode, replace: bool = false) {
    let x1 = (p1.x + .x_scale) * (this.width as f32) / (2.0 * this.x_scale) 
    let y1 = (p1.y + .y_scale) * (this.height as f32) / (2.0 * this.y_scale)
    let x2 = (p2.x + .x_scale) * (this.width as f32) / (2.0 * this.x_scale)
    let y2 = (p2.y + .y_scale) * (this.height as f32) / (2.0 * this.y_scale)

    // line algorithm
    let dx = x2 - x1
    let dy = y2 - y1

    let xt: f32
    let yt: f32

    if dx.abs() >= dy.abs() {
        if x2 < x1 {
            xt = x1; x1 = x2; x2 = xt
            yt = y1; y1 = y2; y2 = yt
        }

        let y = y1
        let inc = (y2 - y1) / (x2 - x1)

        for let x = x1; x <= x2; x += 1.0 {
            if mode == Normal {
                .draw_pixel(x, y, color, replace)
            } else {
                .draw_pixel_aa(x, y, color, replace)
            }

            y += inc
        }

    } else {
        if y2 < y1 {
            xt = x1; x1 = x2; x2 = xt
            yt = y1; y1 = y2; y2 = yt
        }

        let x = x1
        let inc = (x2 - x1) / (y2 - y1)

        for let y = y1; y <= y2; y += 1.0 {
            if mode == Normal {
                .draw_pixel(x, y, color, replace)
            } else {
                .draw_pixel_aa(x, y, color, replace)
            }

            x += inc
        }
    }
}

def Scene::draw_pixel_aa(&this, x: f32, y: f32, color: FColor, replace: bool) {
    let xx = x.round()
    let yy = y.round()

    for let i = xx - 1; i <= xx + 1; i++ {
        for let j = yy - 1; j <= yy + 1; j++ {
            let W = (-(((x - i as f32) * (x - i as f32)) + ((y - j as f32) * (y - j as f32))) * 0.5).exp()

            if 0i32 <= i < .width as i32 and 0i32 <= j < .height as i32 {
                let col = color
                if not replace {
                    let oc = .img.get(i as u32, j as u32)
                    col = FColor(
                        col.r + oc.r * W,
                        col.g + oc.g * W,
                        col.b + oc.b * W
                    )
                }
                .img.set(i as u32, j as u32, col)
            }
        }
    }
}

def Scene::draw_pixel(&this, x: f32, y: f32, color: FColor, replace: bool) {
    let i = x.round()
    let j = y.round()

    if (0i32 <= i < .width as i32 and 0i32 <= j < .height as i32) {
        let col = color
        if not replace {
            let oc = .img.get(i as u32, j as u32)
            col = FColor(
                col.r + oc.r,
                col.g + oc.g,
                col.b + oc.b
            )
        }
        .img.set(i as u32, j as u32, col)
    }
}
